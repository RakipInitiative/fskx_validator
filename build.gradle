buildscript {

    ext.tomcat_plugin_version = '2.4.2'
    ext.tomcat_version = '9.0.4'

    repositories {
        jcenter()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.bmuschko:gradle-tomcat-plugin:$tomcat_plugin_version"
    }
}

plugins {
    id "jacoco"
    id "org.sonarqube" version "3.1.1"
}

sonarqube {
    properties {
        property "sonar.projectKey", "RakipInitiative_fskx_validator"
        property "sonar.organization", "rakipinitiative"
        property "sonar.host.url", "https://sonarcloud.io"
        property("sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        property("sonar.sources", "src")
        property("sonar.tests", "test")
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

webAppDirName = 'webapp'

tomcat {
    contextPath = '/'
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
}

tasks.jacocoTestReport {
    dependsOn("test")

    executionData(file("$buildDir/jacoco/test.exec"))
    reports {
        xml.enabled(true)
        html.enabled(true)
    }
}

// For running locally
mainClassName = "io.ktor.server.tomcat.EngineMain"

sourceSets {
    main.kotlin.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
    test.kotlin.srcDirs = ['test']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url = uri("http://mvn.sems.uni-rostock.de/releases/") }

    maven {
        name 'fskml'
        url 'https://maven.pkg.github.com/silebat/fskml'
        credentials {
            username project.findProperty("github.packages.username") ?: System.getenv("GITHUB_NAME")
            password project.findProperty("github.packages.password") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:$tomcat_version",
            "org.apache.tomcat.embed:tomcat-embed-jasper:$tomcat_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-server-tomcat:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-freemarker:$ktor_version"
    implementation "io.ktor:ktor-server-host-common:$ktor_version"

    // Formats
    implementation "de.uni-rostock.sbi:CombineArchive:1.4.0"
    implementation "de.bund.bfr:fskml:1.0.12"

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
}

